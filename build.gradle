buildscript {
    ext {
        versions = [
            kotlin:'1.3.71',
            serialization:'0.20.0',
            ktor:'1.3.2',
            nodePlugin:'2.2.3',
            carp:'1.0.0-alpha.14',
        ]
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"

        // JS plugins.
        classpath "com.github.node-gradle:gradle-node-plugin:${versions.nodePlugin}"
    }

    repositories {
        jcenter()

        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

group 'dk.cachet.carp.httpclient'
version "${versions.carp}"

repositories {
    jcenter()
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'com.github.node-gradle.node'
kotlin {
    jvm()
    js {
        browser {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "io.ktor:ktor-client-core:${versions.ktor}"
                implementation "io.ktor:ktor-client-json:${versions.ktor}"
                implementation "io.ktor:ktor-client-serialization:${versions.ktor}"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:${versions.serialization}"

                api "dk.cachet.carp.studies:carp.studies.core:${versions.carp}"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-client-okhttp:${versions.ktor}"
                implementation "io.ktor:ktor-client-json-jvm:${versions.ktor}"
                implementation "io.ktor:ktor-client-serialization-jvm:${versions.ktor}"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:${versions.serialization}"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "io.ktor:ktor-client-js:${versions.ktor}"
                implementation "io.ktor:ktor-client-json-js:${versions.ktor}"
                implementation "io.ktor:ktor-client-serialization-js:${versions.ktor}"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:${versions.serialization}"

                // HACK: This dependency should not be required but is currently needed to make the JS build pass:
                // https://github.com/Kotlin/kotlinx-io/issues/57#issuecomment-573144039
                // We should try removing it after upgrading to newer versions of ktor, which seems to be causing this.
                implementation npm("text-encoding", '0.7.0')
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}
