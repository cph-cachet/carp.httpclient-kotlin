buildscript {
    ext {
        versions = [
            kotlin:'1.3.61',
            serialization:'0.14.0',
            ktor:'1.3.0',
            carp:'1.0.0-alpha.10',
        ]
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
    }

    repositories {
        jcenter()
    }
}

group 'dk.cachet.carp.httpclient'
version "${versions.carp}"

repositories {
    jcenter()
}

apply plugin: 'kotlin-multiplatform'
kotlin {
    jvm()
    js {
        browser {
        }
    }
    sourceSets {
        commonMain {
            dependencies {
                implementation kotlin('stdlib-common')
                implementation "io.ktor:ktor-client-core:${versions.ktor}"
                implementation "io.ktor:ktor-client-serialization:${versions.ktor}"
                implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime-common:${versions.serialization}"

                api "dk.cachet.carp.studies:carp.studies.core:${versions.carp}"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test-common')
                implementation kotlin('test-annotations-common')
            }
        }
        jvmMain {
            dependencies {
                implementation kotlin('stdlib-jdk8')
                implementation "io.ktor:ktor-client-okhttp:${versions.ktor}"
            }
        }
        jvmTest {
            dependencies {
                implementation kotlin('test')
                implementation kotlin('test-junit')
            }
        }
        jsMain {
            dependencies {
                implementation kotlin('stdlib-js')
                implementation "io.ktor:ktor-client-js:${versions.ktor}"
            }
        }
        jsTest {
            dependencies {
                implementation kotlin('test-js')
            }
        }
    }
}
